;;;; -------------------------------------------------------------------------
;;;; Plan

(uiop/package:define-package :asdf/plan
  ;; asdf/action below is needed for required-components, traverse-action and traverse-sub-actions
  ;; that used to live there before 3.2.0.
  (:recycle :asdf/plan :asdf)
  (:use :uiop/common-lisp :uiop :asdf/upgrade :asdf/session
        :asdf/component :asdf/operation :asdf/action :asdf/lisp-action
        :asdf/system :asdf/system-registry :asdf/find-component)
  (:export
   #:component-operation-time
   #:plan #:plan-traversal #:sequential-plan #:*plan-class*
   #:action-status #:action-stamp #:action-done-p #:action-index #:action-planned-p
   #:action-already-done-p #:+action-status-out-of-date+
   #:circular-dependency #:circular-dependency-actions
   #:needed-in-image-p
   #:record-dependency
   #:normalize-forced-systems #:action-forced-p #:action-forced-not-p
   #:map-direct-dependencies #:reduce-direct-dependencies #:direct-dependencies
   #:compute-action-stamp #:traverse-action
   #:make-plan #:plan-actions #:perform-plan #:plan-operates-on-p
   #:planned-p #:index #:forced #:forced-not
   #:plan-actions-r
   #:required-components #:filtered-sequential-plan
   #:plan-system
   #:plan-component-type #:plan-keep-operation #:plan-keep-component
   ))
(in-package :asdf/plan)

;;;; Generic plan traversal class
(with-upgradability ()
  (defclass plan () ()
    (:documentation "Base class for a plan based on which ASDF can build a system"))
  (defclass plan-traversal (plan)
    (;; Can this plan be performed? A plan that has different force and force-not settings than the
     ;; session plan can only be used for read-only queries that do not cause the status of any
     ;; action to be raised.
     (performable-p :initform nil :initarg :performable-p :reader plan-performable-p)
     ;; The parent plan from which this plan inherits forced status, etc.
     (parent :initform nil :initarg :parent :reader plan-parent)
     ;; The system for which the plan is computed
     (system :initform nil :initarg :system :accessor plan-system)
     ;; Table of systems specified via :force arguments
     (forced :initform nil :initarg :force :accessor forced)
     ;; Table of systems specified via :force-not argument (and/or immutable)
     (forced-not :initform nil :initarg :force-not :accessor forced-not))
    (:documentation "Base class for plans that simply traverse dependencies")))


;;;; Planned action status
(with-upgradability ()
  (defclass action-status ()
    ((stamp
      :initarg :stamp :reader action-stamp
      :documentation "STAMP associated with the ACTION if it has been completed already
in some previous image, or T if it needs to be done.")
     (done-p
      :initarg :done-p :reader action-done-p
      :documentation "a boolean, true iff the action was already done (before any planned action).")
     (planned-p
      :initarg :planned-p :initform nil :reader action-planned-p
      :documentation "a boolean, true iff the action was included in the plan.")
     (index
      :initarg :index :initform nil :reader action-index
      :documentation "an integer, counting all traversed actions in traversal order, or NIL."))
    (:documentation "Status of an action in a plan"))

  (defparameter +action-status-out-of-date+ (make-instance 'action-status :stamp t :done-p nil))

  (defun make-action-status (&key stamp done-p planned-p index)
    (if (eq stamp t)
        +action-status-out-of-date+
        (make-instance 'action-status :stamp stamp :done-p done-p :planned-p planned-p :index index)))

  (defmethod print-object ((status action-status) stream)
    (print-unreadable-object (status stream :type t)
      (with-slots (stamp done-p planned-p index) status
        (format stream "誉"
                `(:stamp ,stamp :done-p ,done-p
                         ,@(when planned-p `(:planned-p ,planned-p))
                         ,@(when index `(:index ,index)))))))

  (defgeneric action-status (plan operation component)
    (:documentation "Returns the ACTION-STATUS associated to
the action of OPERATION on COMPONENT in the PLAN"))

  (defgeneric (setf action-status) (new-status plan operation component)
    (:documentation "Sets the ACTION-STATUS associated to
the action of OPERATION on COMPONENT in the PLAN"))

  (defun action-already-done-p (plan operation component)
    "According to this plan, is this action already done and up to date?"
    (action-done-p (action-status plan operation component)))

  (defmethod action-status ((plan null) (o operation) (c component))
    (multiple-value-bind (stamp done-p) (component-operation-time o c)
      (make-action-status :stamp stamp :done-p done-p)))

  (defmethod (setf action-status) (new-status (plan null) (o operation) (c component))
    (let ((times (component-operation-times c)))
      (if (action-done-p new-status)
          (remhash o times)
          (setf (gethash o times) (action-stamp new-status))))
    new-status))

;;;; forcing
(with-upgradability ()
  (defgeneric action-forced-p (plan operation component)
    (:documentation "Is this action forced to happen in this plan?"))
  (defgeneric action-forced-not-p (plan operation component)
    (:documentation "Is this action forced to not happen in this plan?
Takes precedence over action-forced-p."))

  (defun normalize-forced-systems (force system)
    "Given a SYSTEM on which operate is called and the specified FORCE argument,
extract a hash-set of systems that are forced, or a predicate on system names,
or NIL if none are forced, or :ALL if all are."
    (etypecase force
      ((or (member nil :all) hash-table function) force)
      (cons (list-to-hash-set (mapcar #'coerce-name force)))
      ((eql t) (when system (list-to-hash-set (list (coerce-name system)))))))

  (defun normalize-forced-not-systems (force-not system)
    "Given a SYSTEM on which operate is called, the specified FORCE-NOT argument,
and the set of IMMUTABLE systems, extract a hash-set of systems that are effectively forced-not,
or predicate on system names, or NIL if none are forced, or :ALL if all are."
    (let ((requested
            (etypecase force-not
              ((or (member nil :all) hash-table function) force-not)
              (cons (list-to-hash-set (mapcar #'coerce-name force-not)))
              ((eql t) (if system (let ((name (coerce-name system)))
                                    #'(lambda (x) (not (equal x name))))
                           :all)))))
      (if (and *immutable-systems* requested)
          #'(lambda (x) (or (call-function requested x)
                            (call-function *immutable-systems* x)))
          (or *immutable-systems* requested))))

  ;; TODO: shouldn't we be looking up the primary system name, rather than the system name?
  (defun action-override-p (plan operation component override-accessor)
    "Given a plan, an action, and a function that given the plan accesses a set of overrides
(i.e. force or force-not), see if the override applies to the current action."
    (declare (ignore operation))
    (call-function (funcall override-accessor plan)
                   (coerce-name (component-system (find-component () component)))))

  (defmethod action-forced-p (plan operation component)
    (and
     ;; Did the user ask us to re-perform the action?
     (action-override-p plan operation component 'forced)
     ;; You really can't force a builtin system and :all doesn't apply to it,
     ;; except if it's the specifically the system currently being built.
     (not (let ((system (component-system component)))
            (and (builtin-system-p system)
                 (not (eq system (plan-system plan))))))))

  (defmethod action-forced-not-p (plan operation component)
    ;; Did the user ask us to not re-perform the action?
    ;; NB: force-not takes precedence over force, as it should
    (action-override-p plan operation component 'forced-not))

  (defmethod action-forced-p ((plan null) (operation operation) (component component))
    nil)

  (defmethod action-forced-not-p ((plan null) (operation operation) (component component))
    nil))


;;;; Is the action needed in this image?
(with-upgradability ()
  (defgeneric needed-in-image-p (operation component)
    (:documentation "Is the action of OPERATION on COMPONENT needed in the current image
to be meaningful, or could it just as well have been done in another Lisp image?"))

  (defmethod needed-in-image-p ((o operation) (c component))
    ;; We presume that actions that modify the filesystem don't need be run
    ;; in the current image if they have already been done in another,
    ;; and can be run in another process (e.g. a fork),
    ;; whereas those that don't are meant to side-effect the current image and can't.
    (not (output-files o c))))


;;;; Visiting dependencies of an action and computing action stamps
(with-upgradability ()
  (defun* (map-direct-dependencies) (operation component fun)
    "Call FUN on all the valid dependencies of the given action in the given plan"
    (loop* :for (dep-o-spec . dep-c-specs) :in (component-depends-on operation component)
      :for dep-o = (find-operation operation dep-o-spec)
      :when dep-o
      :do (loop :for dep-c-spec :in dep-c-specs
            :for dep-c = (and dep-c-spec (resolve-dependency-spec component dep-c-spec))
            :when (action-valid-p dep-o dep-c)
            :do (funcall fun dep-o dep-c))))

  (defun* (reduce-direct-dependencies) (operation component combinator seed)
    "Reduce the direct dependencies to a value computed by iteratively calling COMBINATOR
for each dependency action on the dependency's operation and component and an accumulator
initialized with SEED."
    (map-direct-dependencies
     operation component
     #'(lambda (dep-o dep-c) (setf seed (funcall combinator dep-o dep-c seed))))
    seed)

  (defun* (direct-dependencies) (operation component)
    "Compute a list of the direct dependencies of the action within the plan"
    (reverse (reduce-direct-dependencies operation component #'acons nil)))

  ;; In a distant future, get-file-stamp, component-operation-time and latest-stamp
  ;; shall also be parametrized by the plan, or by a second model object,
  ;; so they need not refer to the state of the filesystem,
  ;; and the stamps could be cryptographic checksums rather than timestamps.
  ;; Such a change remarkably would only affect COMPUTE-ACTION-STAMP.

  (defmethod compute-action-stamp (plan (o operation) (c component) &key just-done)
    ;; Given an action, figure out at what time in the past it has been done,
    ;; or if it has just been done, return the time that it has.
    ;; Returns two values:
    ;; 1- the TIMESTAMP of the action if it has already been done and is up to date,
    ;;   or T is either hasn't been done or is out of date.
    ;; 2- the DONE-IN-IMAGE-P boolean flag that is T if the action has already been done
    ;;   in the current image, or NIL if it hasn't.
    ;; Note that if e.g. LOAD-OP only depends on up-to-date files, but
    ;; hasn't been done in the current image yet, then it can have a non-T timestamp,
    ;; yet a NIL done-in-image-p flag: we can predict what timestamp it will have once loaded,
    ;; i.e. that of the input-files.
    (nest
     (block ())
     (let ((dep-stamp ; collect timestamp from dependencies (or T if forced or out-of-date)
             (reduce-direct-dependencies
              o c
              #'(lambda (o c stamp)
                  (if-let (it (action-status plan o c))
                    (latest-stamp stamp (action-stamp it))
                    t))
              nil)))
       ;; out-of-date dependency: don't bother expensively querying the filesystem
       (when (and (eq dep-stamp t) (not just-done)) (return (values t nil))))
     ;; collect timestamps from inputs, and exit early if any is missing
     (let* ((in-files (input-files o c))
            (in-stamps (mapcar #'get-file-stamp in-files))
            (missing-in (loop :for f :in in-files :for s :in in-stamps :unless s :collect f))
            (latest-in (stamps-latest (cons dep-stamp in-stamps))))
       (when (and missing-in (not just-done)) (return (values t nil))))
     ;; collect timestamps from outputs, and exit early if any is missing
     (let* ((out-files (remove-if 'null (output-files o c)))
            (out-stamps (mapcar (if just-done 'register-file-stamp 'get-file-stamp) out-files))
            (missing-out (loop :for f :in out-files :for s :in out-stamps :unless s :collect f))
            (earliest-out (stamps-earliest out-stamps)))
       (when (and missing-out (not just-done)) (return (values t nil))))
     (let* (;; There are three kinds of actions:
            (out-op (and out-files t)) ; those that create files on the filesystem
            ;;(image-op (and in-files (null out-files))) ; those that load stuff into the image
            ;;(null-op (and (null out-files) (null in-files))) ; placeholders that do nothing
            ;; When was the thing last actually done? (Now, or ask.)
            (op-time (or just-done (component-operation-time o c)))
            ;; Time stamps from the files at hand, and whether any is missing
            (all-present (not (or missing-in missing-out)))
            ;; Has any input changed since we last generated the files?
            (up-to-date-p (stamp<= latest-in earliest-out))
            ;; If everything is up to date, the latest of inputs and outputs is our stamp
            (done-stamp (stamps-latest (cons latest-in out-stamps))))
       ;; Warn if some files are missing:
       ;; either our model is wrong or some other process is messing with our files.
       (when (and just-done (not all-present))
         (warn "A completed without :[*;*its input file:p2:*誉蔺痕矧蔺痕箕轸秕麴豸骈戾吼埠誉茛ㄡ泗轱瞽溴筱蜷痿轱悌黹篌轭绛轭戾铉翳黹篌轭绛轭ㄡ钿黹篌轭绛轭黹篌轭绛秕舂黹篌轭绛秕戾铉翳黹篌轭绛秕舂┅换物翦翳狒麇躞篝犴鸺轭篝遽镦篝犴鸺麸痨狴铋沐鏖翳珏铄蜥翦骈戾螽换令蜥沐泔钿轸轱轶轭趄轭箝麸翳扉黹翦糸礤篝犴蝈箫祯糸镱ㄩ矧牾篝滹铄换澡滹铄篝犴轶鲠扉浜殒麇蝈牾篝滹铄矧换殒犰骈戾簌篝屙彐驽泗狎躔麸溽翦犷翳弪濮铒轭鲠扉溽糸铉蝈狍镱ㄡ钿犰飙痱弩孱躔麸溽翦镳弪狒轱瞽滹铄悌铒ㄡ泗轱瞽骘蜚邃痨犷悌┅鲠祯弩滹铄篝犴换蝈趱蝾翳栳蜾遽蝾邃糸礤篝犴矧牾篝滹铄秕舡镳换骈戾泸遽糸铉镳轶滹铄麒孱犰骈戾狎躔麸溽翦换轫徵瀛彐驽泗轭痨徙彖镬溴镳轶滹铄麒孱轸麽徙趱犰禊蝓瞵ㄡ钿镳糸礤ㄥ耢镳糸礤滹铄篝犴皓┅换鏖翳翳磲翥栝铉篝犴换滹铄篝犴轭鲠扉浜蝈趱蝾糸礤篝犴轭犷轭溴骈铋翦骢趱蝈徙糸镱铒滹铄弭鲠祯弩铋飑┅┅换换清铄蜷篚痧矧骘痨犷趄狯弪筢鏖翳躔珧徜徕殪轸īㄤ彐礤翳镤轭轸獒扉瀛轭篝犷沐横骠弪è痨犷痨犷趄狯弪筢飑脲骘蜚骘蜚瀛铒簌篝屙犰祜鳝雉桢颦脲螬鏖翳箪雉ㄦ矧沐骘蜚邃铒舂痨犷箦翩骘蜚邃铒蝽犰辁瀛骘蜚邃簌篝屙骘蜚簌篝屙┅箦翩骘蜚邃铒铒蝽犰辁瀛骘蜚邃铒舡簌篝屙骘蜚瀛铒簌篝屙┅┅ㄤ彐珏铄蜷痨犷徙糸镱痨犷ê滹沲礤铘狒轱⑴趄徙骝镯痨犷扉篝镦徙糸镱麸疱蜴矧轭箦聃孱沐┅ㄤ彐礤翳镤痨犷徙糸镱è痨犷扉篝┅痨犷ㄤ彐礤翳镤箦翩徙糸镱篝狒躞铄鳝篝狒躞痨犷镳弪狒轱瞟ㄣ泔眇镱孱舂箦翩ㄧ弭栳箬ㄣ镱悌鲩箝翦洵徙糸镱狍滏箦篌轱瞠┅铄鳝篝狒躞┅ㄤ彐礤翳镤徙糸镱篝狒躞è痨犷镳弪狒轱瞟ㄣ泔眇镱孱舂ㄩㄡ泗轱瞽骘蜚邃铒舡悌ㄡ泗轱瞽篝狒躞铋悌鲠祯弩ㄧ弭栳箬ㄣ镱悌鲩箝翦洵徙糸镱狍滏箦篌轱瞠┅┅ㄤ彐珏铄蜷蝈泔蜾溴疱钿孱泫痨犷镳弪狒轱泔眇镱孱舂ê滹沲礤铘狒轱⒁邈矧犷徙糸镱狍溴疱钿孱泫轭翳沲蝌孱痨犷┅换换零趱犰趄狯弪筢旌趄狯弪箦徙糸镱鏖翳躔珧徜徕殪轸īㄤ彐珏铄蜷趄狯弪箦徙糸镱痨犷镳弪狒轱泔眇镱孱铄邃邃轭轫徵瀛皓换砸林乓优撩陨衔轭翳泔铘屮镦玳鲥刑廖镡赍泗翳狒徙沲眭灬翦溴疱钿孱泫溽翎换鲩箝趔翳徙糸镱溴骈铄怡轸闲乓猎上犷孟托衔盼狎珲礤铘蟋换犷犰轸趄犷箝糸鲥溴疱钿孱汩弩躅戾篌犰蝈徜鲩箝翦洎换轭翳泔铘屮镦翳徙糸镱忮轭矧铒舂闻拍拍晌赏燎怒鞋换楫瀹铄邃麸忮滹铄轭翳沲蝌孱轫徵鲶礤蝈禊栳鲥忮孱滹铄轭痱弼轱躞轫徵瀹换骑徙糸镱翳狒狎躔麸溽翦轸蝈趱蝾釉镣殇孱糸纟轭翳篝狒镦翳徙糸镱换翳狒糸礤篝犴瓞怩轸泔蹯忮泸痿镧蜥痂殂溟珏篝轭箫礤劣钠屮翦铙轱瞟换矧殒翳徙糸镱铄邃麸忮滹铄徵衢町换换物翦翳狒骘犷孛致扉脲痨犷鏖翳镱瀛轫徵瀛疱颦骈戾秕麴豸糸铉徙糸镱换翳忮祜礤翳镤黠蹯忮轭篚骀殂殄铘箝钽轸狍篚礤箝铉戾轫徵换麸趄狯弪箦遽汨铒溴狒盹篝赭殂寤铒瞽铋轲徙糸镱黠蹯忮趄狯弪箦镱禊镱沐换怩铋轲铒溴泔蹯忮趄狯弪箦镱沐疱轫徵瀣楫瀹镱沐痨躞镱沐疱铒瞽铋轲徙糸镱换柔钿戾葡颐拍蜗院轸磲脲犷徙糸镱蝈趱蝾轸沲蝌孱糸礤篝犴狍篝狒躞ㄤ彐礤翳镤徙糸镱篝狒躞横蝻躅è痨犷痨犷镳弪狒轱泔眇镱孱舂换韵南箬秕熹麇轭篝遽翦篝箫礤翳轭扉脲换ㄡ泗轱瞽骘蜚邃铒舡痨犷镳弪狒轱痱轫狎簌篝屙泔眇镱孱舂ㄩㄡ泗轱瞽骘蜚邃铒舡痨犷镳弪狒轱泔眇镱孱舂戾è篝狒躞ㄡ泗轱瞽篝狒躞铋镳弪狒轱泔眇镱孱舂┅ㄩㄡ钿篝狒躞ㄡ泗轱瞽滹铄篝狒躞┅篝狒躞戾è篝犴ㄡ钿篝狒躞ㄡ泗轱瞽篝犴篝狒躞┅┅磲脲徙糸镱篝狒躞轰镱瀛后翎眇篝犴皓┅ㄣ犰飙铄舡礤翳镤┅换拈骀弪孱趄狯弪筢祗换清翳溴疱钿孱汩弩ㄦ殪翦蝈洎滹瞌汨犷珏犷篝狒躞换描邈麒弭桢箫礤ㄤ彐簌篝屙溴疱钿蟓镱咯溴疱钿孱汩弩狎躔麸溽翦换描邈翳狒箫礤溴疱钿孱汩弩狎躔麸溽翦翳秕玷铒铄沐篌狎殪轭轫徵换描邈翳狒箫礤溴疱钿孱汩弩狎躔麸溽翦犷轭轫徵瀣矧徜翳屙麸翳痨犷ㄤ彐躅徙糸镱躔麸溽翦痨犷镳弪狒轱泔眇镱孱舂⒚桢汶麒弭桢铒溴轶躔麸溽翦犷磲螂轸箫殒篚汨迈滹瞌徜犷翳轭麸翳痨犷麒殪瀛鲩箝糸铉徙糸镱镳弪狒轱泔眇镱孱舂磲轭翎轭泔铘屮衄栳钿戾汩蜚蹯狎轸ㄢ祜汶铋换娘蜗蝈泔蜾翳溴疱钿孱泫轸黹玷忮秕镦溽翦戾è篝狒躞ㄡ泗轱瞽篝狒躞痨犷镳弪狒轱泔眇镱孱舂┅麒孱篝狒躞蝈趱蝾铒ㄥㄡ泗轱瞽篝犴篝狒躞舂┅┅戾舄è溴疱钿孱汩弩躔麸溽翦ㄢ祜汶铋磲瓠溟蝈泗溴疱钿孱汩弩蝈沲蝮轹屐趄狯弪箦溴疱钿孱汩弩镳弪狒轱泔眇镱孱＇灬礅溽悌躅戾篌ㄡ泗轱瞽躔麸溽翦痨犷悌蝈趱蝾铋飑┅舂篝狒躞ㄩ溴疱钿孱汩弩躔麸溽翦换涉轸犰躔麸溽翦眭祠轲戾鲠祯瀛忾钿篝犴滹铄皓ㄣ镯瘐翦徙糸镱篝犴痨犷镳弪狒轱泔眇镱孱舂泔眇豸徙糸镱篝犴韵南铒痨犷磲脲徙糸镱篝狒躞后翎眇篝犴轰镱瀛滹铄皓徙糸镱篝狒躞秕舡镦溽翦┅箦翩ㄡ泗轱瞽篝狒躞痨犷镳弪狒轱泔眇镱孱舂篝狒躞铒ㄥㄡ泗轱瞽篝犴篝狒躞舂┅┅ㄤ彐礤翳镤趄狯弪箦徙糸镱痨犷镳弪狒轱泔眇镱孱铄邃邃轭轫徵瀛皓麒殪瀛鲩箝糸铉徙糸镱镳弪狒轱泔眇镱孱舂磲轭翎轭泔铘屮衄栳钿戾汩蜚蹯狎轸ㄢ祜汶铋换义泔蜾翳溴疱钿孱泫澡轶栾镫轶铄邃邃怡邢烧麒殂趄徙塍骢祆溴疱钿孱泫珧狃璎换轭篝遽镦牾篝溴疱钿孱泫矧溴狍轭鲠铋祆劣钠换韵南婶轶犰箫铄邃邃麸溴翦泗闲乓猎怒轭信移弦彤蝈泔蜾溴疱钿孱泫痨犷镳弪狒轱泔眇镱孱舂换铄邃邃轭轫徵溟篝轭珲轶桢獐翳轭珞翳狒眭篝栳痧孱轭翳换沲蝌孱轫徵犷翳矬翳轭珞翳狒箝眇禊铄邃麸栳鲥忮孱滹铄轭痱弼轱躞镱瀹戾舄è犷殚铄邃邃轭轫徵瀛镳弪狒轱泔眇镱孱舂徙糸镱箴邈殒殂铄邃邃轭轫徵换彐驽泗轹铋轲礤犷轭珂蹯骘翳徙糸镱犷蝈聃轵邃怡翳痨犷狍趄狯弪箦ㄥ铋轲ㄡ钿犷殚铄邃邃轭轫徵瀛皓换篝狒躞栳鲥麇趄狯弪箦翳狒徙糸镱痱弼轱躞禊犷殒箫麒狒麽轸篝狒躞篝狒躞ㄡ泗轱瞽篝狒躞痨犷镳弪狒轱泔眇镱孱舂┅麒孱ㄡ钿篝狒躞矧ㄡ泗轱瞽滹铄篝狒躞ㄡ泗轱瞽痨犷铄洵篝狒躞铒孱殚皓┅蝈趱蝾ㄡ泗轱瞽篝犴篝狒躞┅领蝈徜鲩箝翦鏖翳篚骀殂殄铘铄邃轭轫徵戾鲥臁灬忮祗è鲩箝舡徙糸镱铋轲族磲鲩箝翳徙糸镱赭殂瀣镱沐鏖翳铋轲紊态翳孱磲瓠溟蝈泗溴疱钿孱汩弩蝈沲蝮轹屐趄狯弪箦溴疱钿孱汩弩镳弪狒轱泔眇镱孱＇灬礅溽悌趄狯弪箦徙糸镱痨犷铋轲┅眭祠轲戾鲠祯瀛忾钿篝犴滹铄皓疗耘溴疱钿孱汩弩栳鲥忮孱趄狯弪箦洮ㄣ镯瘐翦徙糸镱篝犴痨犷镳弪狒轱泔眇镱孱舂泔眇豸徙糸镱篝犴戾è徜洵麸痨犷矧ㄥ耢篝犴舂ㄡ钿铋轲铒滹铄皓┅┅ㄣ镱轸铄邃忮滹铄殒轸秕镦溽翦矧铄邃邃轭轫徵怩徕箦铘è犷徜洵麸痨犷铒铋轲┅殒麇铄邃麸滹轸鲩箝舡徙糸镱舂翳孱麇铄邃麸滹轸轭翳ㄣ躜蝈铘轫徵濯箦翩ㄡ泗轱瞽篝狒躞痨犷镳弪狒轱泔眇镱孱舂躔溽翦篝狒躞磲脲轭篝犷沐п泗轱瞽篝狒躞后翎眇篝犴泔眇豸邃篝犴轰镱瀛ㄡ钿滹铄铒徜洵麸痨犷皓滹铄犷洫躔麸溽翦吼灬铑邃徜洵麸痨犷轭沆蹁邃轭扉篝镦翳轭珞麸忮滹铄洪钿屮ㄩ篝狒躞换轭溴镦徙糸镱犴镱犰铒溴轭趄狯弪筢ㄡ泗轱瞽轭溴篝狒躞换脲屦轭溴殒犰蝈徜鲩箝翦换屐箦犰祜汜翦铄箦篌轱瞽鏖溴轭溴ㄩ钽麸翎飙徙糸镱泔躅狍滏箦篌轱瞠┅┅麒孱ㄡ钿滹铄铒徜洵麸痨犷皓箦翩ㄣ镯痫铄铘镳弪狒轱瞽糸礤镳弪狒轱泔眇镱孱舂篝犴皓麒孱徜洵麸痨犷殒轸铄邃麸忮徜溴麸翳痨犷ㄩ钽痨犷铄洵徙糸镱泔躅狍滏箦篌轱瞠┅泔躅轸躅戾篌犷殚殒轸秕麴豸痱镤蹉轭绗ㄩ钽痨犷铄洵秕麴豸徙糸镱泔躅狍滏箦篌轱瞠┅┅泔躅轸篝犴皓┅┅蝈趱蝾翳篝犴鲩箝舡徙糸镱孱殚皓┅┅┅鲩箝翳徙糸镱换换渝聃孱糸犰痨犷翳溴驷蹯舂鏖翳躔珧徜徕殪轸īㄤ彐沆狍箦聃孱糸犰痨犷痨犷趄狯弪筢飑è徙糸镱蟓洪铋翩矧铋横沣弩箫痨犷徙糸镱蟓颟ê滹沲礤铘狒轱⒂轫痨弩衄溴驷蹯痨犷沆狍蟋徙沲眭灬糸铉箦聃孱沐镦徙糸镱螈┅ㄤ彐礤翳镤痨犷徙糸镱è痨犷箦聃孱糸犰痨犷┅蝈鲥蝮痨犷徙糸镱蟓痨犷┅换物铄邃麸蝈泔蜾溴疱钿孱泫麸怩殪骢祆珧狃璎牾篝徙沲眭灬翦铒溴轭矧溴虍ㄤ彐礤翳镤蝈泔蜾溴疱钿孱泫è痨犷箦聃孱糸犰痨犷镳弪狒轱瞟ㄣ泔眇镱孱舂鲠祯弩┅ㄤ彐礤翳镤箦翩徙糸镱篝狒躞横骠弪铄鳝篝狒躞箦聃孱糸犰痨犷镳弪狒轱瞟ㄣ泔眇镱孱舂麒孱ㄡ泗轱瞽痨犷铄洵铄鳝篝狒躞瘐箬磲脲徙糸镱悌痨犷徙糸镱蟓皓┅┅换换乳玷戾鲥轭翦蜴徙搴趄狯弪箦疱蜴矧憝痨犷痨犷镳弪狒弩镱鏖翳躔珧徜徕殪轸īㄤ彐珏铄蜷磲脲痨犷痨犷沆狍镳弪狒轱泔眇镱孱脲犰祜鳝雉桢颦脲螬ê滹沲礤铘狒轱⑶孱弪狒犷蝈趱蝾痨犷骘疱蜴矧黹铉闲乓猎上镱孟托衔盼援┅ㄤ彐轭瀛泔铞孱殄钽瀛徙糸镱礤翳镤磲脲痨犷痨犷沆狍镳弪狒轱泔眇镱孱脲┅ㄤ彐珏铄蜷疱蜴矧憝痨犷痨犷脲ê滹沲礤铘狒轱⒘泗踽祆疱蜴矧痨犷犷怩殪翳蝈聃弩翦徙糸镱螈┅ㄤ彐珏铄蜷痨犷镳弪狒弩镱痨犷泔眇镱孱舂ê滹沲礤铘狒轱⒛镥翳轶刑廖轭沆蹁犷镳弪狒轱镱玳鲥孟托衔盼钥┅ㄤ彐疳蜥礤翦颡痨犷沆狍螵箦聃孱糸犰痨犷⒃桢溴驷蹯痨犷沆狍麸躞麒孱怩殪溟铉鏖翳劣钠ㄤ彐礤翳镤磲脲痨犷痨犷沆狍镳弪狒轱瞟ㄣ泔眇镱孱舂蝈篝脲脲犰祜鳝雉桢颦脲螬鏖翳狍滏箦篌轱ī戾è痨犷ㄡ痧禊ы犭瀛轭篝犷沐矧痨犷沆狍痨犷沆狍螵后篝屙ㄣ镯痫铄铘簌篝屙悌脲螬┅趄狯弪箦徙糸镱痨犷舂痨犷┅ㄤ彐礤翳镤疱蜴矧憝痨犷横蝻躅è痨犷舂脲戾è疳汶徵濯疳汶徵濯í蝈徜翎忪濯蝈徜翎忪濯┅鏖翳泔眇殪狒轱瞽躅轸ī换忉汶麽蜾泔眇狒殁殪轸ㄣ犰飙铄舡礤翳镤┅┅换秋轭骘蝼狎洮箦溴驽蝌邃麽蝾轭篚痧矧轭扉箴怩殪洚ㄤ彐礤翳镤疱蜴矧憝痨犷è痨犷舂脲犰祜鳝雉桢颦脲螬祜镳烘矧徙糸镱洪痨犷徙糸镱痨犷横ㄡ泗轱瞽镳弪狒轱徙糸镱横ㄡ泗轱瞽泔眇镱孱徙糸镱轰躅戾篌铘璀鲠祯ㄣ镯瘐翦徙糸镱篝犴痨犷悌疱蜴矧憝鏖翳蝈篝狎趔悌┅ㄤ彐礤翳镤痨犷镳弪狒弩镱è痨犷痨犷趄狯弪筢飑ㄣ镯痫铄铘疳翳扉篝┅痨犷镳弪狒弩镱痨犷徙糸镱痨犷泔眇镱孱舡疳翳┅ㄤ彐礤翳镤痨犷镳弪狒弩镱è痨犷扉篝ㄣ镯痫铄铘疳翳扉篝┅ㄦ轭泔眇镱孱舡疳翳磲疸狎п泗轱瞽泔眇镱孱痨犷呼弩у聃犰弘妁с镯痫铄铘骈钿疳翳┅换换深汩溴铘犰趄狯弪筢祗换歪腴铉粕淘乓拍优颜盼陨撂刑廖汜忮躞邃麸瀹绠犰镦翳箫躜沐换骈戾蝈聃轵邃怡怩钿扉铉镳弪狒轱町鏖翳躔珧徜徕殪轸īㄤ彐沆狍骈祠弪邃箦聃孱糸犰痨犷箦聃孱糸犰痨犷è泔眇镱孱舡豉疱洪铋翩矧洪铋翎蜱恒镯痫铄铘豉疱候遽溴痨犷泔眇镱孱舡豉疱脲屦镳弪狒轱洪铋翩矧洪铋翎蜱弘邋瓠镳弪狒轱候遽溴痨犷脲屦镳弪狒轱瞟脲屦泔眇镱孱洪铋翩矧洪铋翎蜱弘邋瓠泔眇镱孱候遽溴痨犷脲屦泔眇镱孱舂ê滹沲礤铘狒轱⒘鲠蜷犷镦优颜盼陨撂刑廖翳狒镱禊蝈泔蜾篚怏弭镦徙糸镱螽┅ㄤ彐礤翳镤轭轸獒扉瀛轭篝犷沐横骠弪è痨犷骈祠弪邃箦聃孱糸犰痨犷脲骘蜚骘蜚瀛铒雉桢颦簌篝屙螬ㄤ邈灬蝈ㄩ珙矧骘蜚骘蜚瀛铒舂换社铒蝈骘蜚犷骘蜚瀛铒衄蝈禊镱雉桢颦簌篝屙蠛换骘蜚趄狯弪筢镦麒狒麇蝈轭翦蝈篝邃轭楫瀹沲蝌孱簌篝屙矧犰箫雉桢蝮换骘蜚瀛铒趄狯弪筢镦麒狒麇蝈铒轭翦蝈篝邃轭楫瀹雉桢簌篝屙躅戾篌雉桢颦簌篝屙螽鏖翳箪雉ㄦ矧沐骘蜚邃铒簌篝屙痨犷箦翩骘蜚邃横祆箦翩骘蜚邃铒铒蝽犰辁瀛骘蜚邃铒舡簌篝屙ㄩ雉桢颦簌篝屙铋舂簌篝屙┅┅ㄤ彐礤翳镤痨犷徙糸镱è痨犷骈祠弪邃箦聃孱糸犰痨犷┅鏖翳箪雉脲屦镳弪狒轱脲屦泔眇镱孱舂痨犷祜镳烘矧徙糸镱洪ㄣ犰飙铄舡礤翳镤横ㄡ泗轱瞽镳弪狒轱徙糸镱横ㄡ泗轱瞽泔眇镱孱徙糸镱瑚桢ㄡ钿豉疱脲屦镳弪狒轱瞟豉疱脲屦泔眇镱孱舂恒镬戾泗ㄣ镱悌┅ㄤ彐躅泔祆邈舡徙糸镱溴疱钿孱汩弩痨犷镳弪狒轱泔眇镱孱舂麒殪瀛鲩箝糸铉徙糸镱镳弪狒轱泔眇镱孱舂磲轭翎轭泔铘屮衄栳钿戾汩蜚蹯狎轸戾è徙糸镱磲脲徙糸镱镳弪狒轱泔眇镱孱舂┅躅戾篌ㄧ弭栳箬徙糸镱鲩箝翦洵徙糸镱狍滏箦篌轱瞠┅箦翩ㄧ弭栳箬徙糸镱鲩箝翦洵徙糸镱狍滏箦篌轱瞠┅舂麒孱ㄡ钿豉疱泔眇镱孱痨犷泔眇镱孱舡豉疱痨犷┅铒ㄡ泗轱瞽骘蜚邃铒舡痨犷镳弪狒轱泔眇镱孱舂┅磲瓠溟蝈泗溴疱钿孱汩弩镳弪狒轱泔眇镱孱＇灬礅溽悌ㄣ镬戾泗徙糸镱溴疱钿孱汩弩痨犷悌┅瘐箬徙糸镱痨犷徙糸镱蟓痨犷┅┅┅ㄤ彐珏铄蜷泔祆邈舡溴疱钿孱汩弩镳弪狒轱泔眇镱孱脲犰祜鳝雉桢颦脲螬ê滹沲礤铘狒轱⑶轹孱犷徙糸镱怩殪痨犷骘犰镦轸溴疱钿孱汩弩┅ㄤ彐轭瀛泔铞孱殄钽瀛徙糸镱礤翳镤泔祆邈舡溴疱钿孱汩弩镳弪狒轱泔眇镱孱脲┅ㄤ彐礤翳镤泔祆邈舡溴疱钿孱汩弩è镳弪狒轱镳弪狒轱瞟ㄣ镯痫铄铘泔眇镱孱舂蝈篝脲脲犰祜鳝雉桢颦脲螬戾è痨犷ㄡ痧禊ы犭瀛轭篝犷沐ф殪翦蝈洵箦聃孱糸犰痨犷后篝屙ㄣ镯痫铄铘簌篝屙泔眇镱孱舂烘矧沐横祆脲螬┅祜镳烘矧徙糸镱洪ㄤ轵邈舡溴疱钿孱汩弩镳弪狒轱泔眇镱孱舂轰ㄣ镬戾泗徙糸镱溴疱钿孱汩弩痨犷ㄡ泗轱瞽镳弪狒轱徙糸镱ㄡ泗轱瞽泔眇镱孱徙糸镱┅痨犷徙糸镱痨犷┅ㄤ彐躅蝈聃轵邃泔眇镱孱趔簌篝屙蝈篝脲脲ㄧ镝飙镳弪狒轱ъ镝洵镳犰祜鳝雉桢颦脲螬⑶轹孱淤釉磐犷窍撂闲乓猎上ㄤ彐狨祠滔聊闲┈趄狯弪箦翳溴疱钿孱汩弩犷蝈趱蝾扉篝镦翳泔眇镱孱趔轭鲲祧邃轭怩殪溟铉翳溴箝蝈徙糸镱戾è汜汨ㄡ钿狍滏箦篌轱瞠箦篌轱瞽汜汨狍滏箦篌轱瞠┅┅鏖翳狍滏箦篌轱ê秭弪蜷溴舂麒孱汜汨箦翩箦篌轱瞽汜汨狍滏箦篌轱瞠汜汨濠蝈盹鲥漉痨殂狒弩磲疸狎п泗轱瞽泔眇镱孱ㄡ痧禊с镬戾泗溴疱钿孱汩弩顼犰镳弪狒轱簌篝屙蝈盹鲥痨轶舡脲虹镝飙镳弪狒轱脲螬┅烘蝻憝孱舂┅┅