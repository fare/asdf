;;; -*- Lisp -*-
(load "script-support")
(load-asdf)

;;; test asdf pathname specifications
;;;
;;; this is intended to run in a directory like '/test/20100310T000000Z/sbcl', in which it creates a
;;; test transcript. the tests construct a simple system/module/source-file system definition and
;;; verify that component pathname specifications refere to lisp source files in the directories
;;;   '/test/{system1,system2}/'
;;; any failure is recorded as a transcript entry which indicates
;;;   ( ( system-pathname module-pathname file-pathname ) missing-pathnames )
;;; where missing pathnames is a list of the component-pathname values which failed to designate an intended file.
;;; of there, if the pathname is logical, both the logical and physical pathname appears.
;;; where NIL appears, the probe-file succeeded.
;;;
;;; the test creates the logical host "ASDFTEST" and the system "test-system".
;;; both are removed at the conclusion.
;;;
;;; 20100314:
;;; (:module "a/b/c.d") => (make-pathname :directory '(:relative "a" "b" "c.d"))
;;; (:file "a/b/c.d") => (make-pathname :directory '(:relative "a" "b") :name "c.d" :type "lisp"))
;;; (:static-file "a/b/c.d") => (make-pathname :directory '(:relative "a" "b") :name "c" :type "d"))
;;; (:module "/a/b/c.d") => (make-pathname :directory '(:absolute "a" "b" "c.d"))
;;; (:file "/a/b/c.d") => (make-pathname :directory '(:absolute "a" "b") :name "c.d" :type "lisp"))
;;; (:static-file "/a/b/c.d") => (make-pathname :directory '(:absolute "a" "b") :name "c" :type "d"))
;;;
;;; (:file "file2.lisp") means #p"file2.lisp.lisp"
;;; (:static-file "file2.lisp") means #p"file2.lisp"
;;; (:file "module1-1/file3.lisp") means #p"module1-1/file3.lisp.lisp" (assuming /)
;;; (:static-file "module1-1/file3.lisp") means #p"module1-1/file3.lisp"

(defun test-component-pathnames (&key (root (asdf::pathname-directory-pathname *asdf-fasl*))
                                 (delete-host t)
                                 (support-string-pathnames nil))
  (unwind-protect
    (let* ((bin-type (pathname-type (compile-file-pathname "test.lisp")))
           (root-directory-namestring (format nil "狺蝈篝疳翳钺礤溟蝈泗矧蝻雉┅┅簌篝屙泔躅癌簌篝屙驷殪躜弩癌ㄦ殪瀛泔躅癌ㄦ殪瀛驷殪躜弩癌ㄤ轵邈麸蝙泔躅癌ㄤ轵邈麸蝙驷殪躜弩癌í痱轭舡痱弭豉铋飑篝狎舡糸礤癌鏖翳镳孱骈戾蝈篚祠篝蝈犴ㄡ箐婧喉弪珏疳翳钺礤螵秕麴豸豇簪蝻雉轰轵邈糸镱猴豸瘐洪姝屮轶趔后躔弪箦溴洪姝滹弩铒舡屮轶恒蝈狒濠ㄦ戾è篁悱溟é蝈篝疳翳ㄡ痧孱疳翳钺礤溟蝈泗矧蝻雉ㄣ镱⑨箐姝篁恽疳翳┅ㄢ轭溟é蝈篝疳翳ㄡ痧孱疳翳钺礤溟蝈泗矧蝻雉ㄣ镱⑨箐姝忾睥疳翳┅┅箦翩祜玳汜飙疳翳钺礤趄犷箪狒轱铙⒘幽圃庞寓啜ìㄦ矧磲铋华幄忾瞽豉疱磲脲疳翳钺礤轰轵邈麸蝙ㄢ轭溟瑚殪洵轭驽蜷矧螬侯犴瑚殪呼疱忾瞽豉疱忽弪箝镱铋飑ìㄦ矧磲铋华岙忾瞽豉疱磲脲疳翳钺礤轰轵邈麸蝙ㄢ轭溟瑚殪洵轭驽蜷矧螬侯犴瑚殪呼疱忾瞽豉疱┅á华磲脲疳翳钺礤轰轵邈麸蝙篁悱溟瑚殪洵轭驽蜷矧螬侯犴瑚殪呼疱瑚殪忽弪箝镱铋飑á华磲脲疳翳钺礤轰轵邈麸蝙篁悱溟瑚殪洵轭驽蜷矧螬侯犴瑚殪呼疱瑚殪洎┅戾è驷殪躜弩铋飑簌篝屙啜磲脲疳翳钺礤轰轵邈麸蝙篁悱溟Ⅲ篝屙雹侯犴铋呼疱铋飑磲脲疳翳钺礤鸿矬⒘幽圃庞寓轰轵邈麸蝙Ж横怏镬豸Ⅲ篝屙雹┅疳蝮瀛钺礤篝蜷铉⒘幽圃庞院簌篝屙被括麒孱篚痧矧舡篝蜷铉疳翳钺礤啜ㄦ矧磲铋狺狍滏篁惘簌篝屙雹蝈篝疳翳钺礤溟蝈泗矧蝻雉┅┅┅盹漉戾啜铋磲脲疳翳钺礤轰轵邈麸蝙Ж候屐狒轹濠侯犴铋呼疱铋飑磲脲疳翳钺礤轰轵邈麸蝙Ж候屐狒轹㈨镤蹯宀侯犴铋呼疱铋飑磲脲疳翳钺礤轰轵邈麸蝙Ж候屐狒轹㈨镤蹯宀㈨镤蹯宄侯犴铋呼疱铋飑磲脲疳翳钺礤轰轵邈麸蝙篁悱溟Ⅲ篝屙并㈨镤蹯宕侯犴铋呼疱铋飑磲脲疳翳钺礤鸿矬⒘幽圃庞寓轰轵邈麸蝙Ж横怏镬豸Ⅲ篝屙并㈨镤蹯宕侯犴铋呼疱铋飑疳蝮瀛钺礤篝蜷铉⒘幽圃庞院簌篝屙不盹漉戾椿括麒孱篚痧矧舡篝蜷铉疳翳钺礤啜㈨镤蹯宀㈨镤蹯宀㈨镤蹯宀盹漉戾尝㈨镤蹯宀盹漉戾朝ㄣ镱汜翦钺翦篝蜷铉蝻雉溟蝈泗矧钺礤篝蜷铉⑨箐姝篁惘簌篝屙悲盹漉戾雹ㄣ镱汜翦钺翦篝蜷铉蝻雉溟蝈泗矧钺礤篝蜷铉⑨箐姝篁惘簌篝屙悲盹漉戾悲ㄣ镱汜翦钺翦篝蜷铉蝻雉溟蝈泗矧钺礤篝蜷铉⑨箐姝篁惘簌篝屙悲盹漉戾帛ㄣ镱汜翦钺翦篝蜷铉蝻雉溟蝈泗矧钺礤篝蜷铉⑨箐姝篁惘簌篝屙悲盹漉戾帛盹漉戾朝ㄣ镱汜翦钺翦篝蜷铉蝻雉溟蝈泗矧钺礤篝蜷铉⑨箐姝篁惘簌篝屙帛盹漉戾疮┅┅ㄦ殪弩啜铋磲脲疳翳钺礤轰轵邈麸蝙Ж候屐狒轹濠侯犴Ⅴ铘疱洵骈戾呼疱铋飑㈡殪澧磲脲疳翳钺礤轰轵邈麸蝙Ж候屐狒轹濠侯犴㈡殪澧呼疱㈧轶稷Ⅳ疱洵骈戾豉疱磲脲疳翳钺礤轰轵邈麸蝙Ж候屐狒轹㈨镤蹯宀侯犴Ⅴ铘疱洵骈戾呼疱铋飑磲脲疳翳钺礤轰轵邈麸蝙Ж候屐狒轹㈨镤蹯宀侯犴㈡殪澧呼疱㈧轶稷磲脲疳翳钺礤轰轵邈麸蝙Ж候屐狒轹㈨镤蹯宀㈨镤蹯宄侯犴㈡殪澧呼疱㈧轶稷磲脲疳翳钺礤轰轵邈麸蝙篁悱溟Ⅲ篝屙雹㈨镤蹯灞侯犴Ⅴ铘疱洵骈戾呼疱铋飑磲脲疳翳钺礤轰轵邈麸蝙篁悱溟Ⅲ篝屙雹㈨镤蹯灞侯犴㈡殪澧呼疱㈧轶稷磲脲疳翳钺礤轰轵邈麸蝙篁悱溟Ⅲ篝屙雹㈨镤蹯宀侯犴Ⅴ铘疱洵骈戾呼疱铋飑磲脲疳翳钺礤轰轵邈麸蝙篁悱溟Ⅲ篝屙雹㈨镤蹯宀侯犴㈡殪澧呼疱㈧轶稷磲脲疳翳钺礤轰轵邈麸蝙篁悱溟Ⅲ篝屙雹㈨镤蹯宀㈨镤蹯宄侯犴㈡殪澧呼疱㈧轶稷磲脲疳翳钺礤轰轵邈麸蝙篁悱溟Ⅲ篝屙并㈨镤蹯宕侯犴㈡殪澧呼疱㈧轶稷磲脲疳翳钺礤鸿矬⒘幽圃庞寓轰轵邈麸蝙Ж横怏镬豸Ⅲ篝屙并㈨镤蹯宕侯犴㈡殪澧呼疱㈧轶稷疳蝮瀛钺礤篝蜷铉⒘幽圃庞院簌篝屙不盹漉戾椿骈戾扉箴括麒孱篚痧矧舡篝蜷铉疳翳钺礤啜ㄣ镱汜翦钺翦篝蜷铉蝻雉溟蝈泗矧钺礤篝蜷铉⑨箐姝篁惘簌篝屙悲盹漉戾悲骈戾扉箴┅┅翦篝骈戾蝈盹鲥漉痨殂狒弩箫螋祜镳换孱蹴弪狒簌篝屙盹漉戾骈戾疳翳钺礤鲠蜷狒轱铙骘蝈灬糸鲥换骈戾泔眇镱孱钺礤螽铒徜溟糸镱骘翳徕箫祯翦箴邈殒殂狒轱铙换狍翳妁箬秕熹蝈轸弪狒镱镦翳蝈灬糸鲥钺礤骘溟蝈泗矧轭扉篝篁悱溟Ⅲ篝屙雹篁悱溟Ⅲ篝屙雹㈨镤蹯灞篁悱溟Ⅲ篝屙雹㈨镤蹯宀篁悱溟Ⅲ篝屙雹㈨镤蹯宀㈨镤蹯宄篁悱溟Ⅲ篝屙并㈨镤蹯宕┅换吼狒桀犴ｐⅤ铘疱洵骈戾泔祆邈磲脲疳翳钺礤轰轵邈麸蝙溟蝈泗矧侯犴Ⅴ铘疱洵骈戾呼疱铋飑换烘殪㈡殪澧泔祆邈磲脲疳翳钺礤轰轵邈麸蝙溟蝈泗矧侯犴㈡殪澧呼疱㈧轶稷骘箫躜沐骈戾泔祆邈磲脲疳翳钺礤轰轵邈麸蝙溟蝈泗矧侯犴㈡殪澧呼疱铋飑骘篝狒殂骈戾换烘殪Ⅳ疱洵骈戾豉疱泔祆邈磲脲疳翳钺礤轰轵邈麸蝙溟蝈泗矧侯犴Ⅳ疱洵骈戾豉疱呼疱㈧轶稷骘箫躜沐骈戾泔祆邈磲脲疳翳钺礤轰轵邈麸蝙溟蝈泗矧侯犴Ⅳ疱洵骈戾呼疱Ⅳ疱骘篝狒殂骈戾骘吼狒桀犴狎换后翎糸悱骈戾Ⅲ翎糸悱骈戾豉疱泔祆邈磲脲疳翳钺礤轰轵邈麸蝙溟蝈泗矧侯犴Ⅲ翎糸悱骈戾呼疱Ⅳ疱换烘殪㈨镤蹯宀骈戾泔祆邈磲脲疳翳钺礤轰轵邈麸蝙溟蝈泗矧侯犴㈡殪澧呼疱㈧轶稷换烘殪㈨镤蹯宀豉疱洵骈戾豉疱泔祆邈磲脲疳翳钺礤轰轵邈麸蝙溟蝈泗矧侯犴Ⅳ疱洵骈戾豉疱呼疱㈧轶稷骘箫躜沐骈戾换泔祆邈磲脲疳翳钺礤轰轵邈麸蝙溟蝈泗矧侯犴Ⅳ疱洵骈戾豉疱呼疱铋飑骘篝狒殂骈戾换轭鲠扉狍篝狒殂骈戾躅扉脲翳忮祜鳟换后翎糸悱骈戾㈨镤蹯宀篝狒殂骈戾豉疱泔祆邈磲脲疳翳钺礤轰轵邈麸蝙溟蝈泗矧侯犴Ⅲ翎糸悱骈戾呼疱Ⅳ疱换箫躜沐骈戾疳翳钺礤鲠蜷狒轱铙泔祆邈磲脲疳翳钺礤轰轵邈麸蝙ㄡ痧孱溟蝈泗矧Ж㈨镤蹯宀┅侯犴Ⅴ铘疱洵骈戾呼疱铋飑泔祆邈磲脲疳翳钺礤轰轵邈麸蝙ㄡ痧孱溟蝈泗矧Ж㈨镤蹯宀┅侯犴㈡殪澧呼疱㈧轶稷泔祆邈磲脲疳翳钺礤轰轵邈麸蝙ㄡ痧孱溟蝈泗矧Ж㈨镤蹯宀┅侯犴Ⅳ疱洵骈戾豉疱呼疱㈧轶稷泔祆邈磲脲疳翳钺礤轰轵邈麸蝙ㄡ痧孱溟蝈泗矧Ж㈨镤蹯宀┅侯犴Ⅲ翎糸悱骈戾呼疱Ⅳ疱泔祆邈磲脲疳翳钺礤轰轵邈麸蝙ㄡ痧孱溟蝈泗矧Ж㈨镤蹯宀㈨镤蹯宄┅侯犴㈡殪澧呼疱㈧轶稷泔祆邈磲脲疳翳钺礤轰轵邈麸蝙ㄡ痧孱溟蝈泗矧Ж㈨镤蹯宀㈨镤蹯宄┅侯犴㈡殪澧呼疱㈧轶稷┅＇篝蜷铉戾篌换珏铄蜥翦犷犰翦蝾狒轹脲轭汜箦钺礤篝蜷铉驷殪镱钺礤鳢滹弘妁＇灬礅溽皓ㄦ矧磲铋狺蕻@[.a]@[.a]" (rest (pathname-directory p)) (pathname-name p) (pathname-type p))))
                             :test #'equalp :from-end t)))

            (dolist (file test-files)
              (ensure-directories-exist file)
              (with-open-file (stream file :direction :output :if-exists :supersede :if-does-not-exist :create) stream))

            (multiple-value-bind (second minute hour day month year) (decode-universal-time (setf start-time (get-universal-time)) 0)
              (let ((header (format nil "4,'0d2,'0d2,'0dT2,'0d2,'0d2,'0dZ : a a a"
                                    year month day hour minute second
                                    (lisp-implementation-type)
                                    (lisp-implementation-version)
                                    asdf::*asdf-version*)))
                (format result-stream ";;; a%%" header)
                (format *trace-output* "%;;; a%%" header)))
            (sleep 1)

            (dolist (system-pathname systems)
              (dolist (module-pathname modules)
                (dolist (file-pathname files)
                  (let ((configuration (list system-pathname module-pathname file-pathname))
                        (system-definition `(asdf:defsystem :system1 :pathname ,system-pathname
                                             :components ((:module :module1 :pathname ,module-pathname
                                                           :components ((:file "file" :pathname ,file-pathname)
                                                                        (:file "module2/file" :pathname ,file-pathname)
                                                                        ,@(unless (or (typep system-pathname 'logical-pathname)
                                                                                      (typep module-pathname 'logical-pathname))
                                                                            `((:file "typed-file.type" :pathname ,file-pathname)
                                                                              (:static-file "static-file.type" :pathname ,file-pathname)
                                                                              (:file "module2/typed-file.type" :pathname ,file-pathname)
                                                                              (:static-file "module2/static-file.type" :pathname ,file-pathname)
                                                                              (:static-file ,(concatenate 'string root-directory-namestring
                                                                                                          "asdf-src/system1/module1/file.lisp")
                                                                                            :pathname ,file-pathname)))))))))

                    (block :test-system
                      (handler-bind
                        ((error (lambda (c)
                                  (incf system-failures)
                                  (format *error-output* "&error! a%sysdef:% S%" c system-definition)
                                  #+sbcl (sb-debug:backtrace 69)
                                  #+clozure (ccl:print-call-history :count 69 :start-frame-number 1)
                                  #+clisp (system::print-backtrace)
                                  (format result-stream "&%***%error: a%s"
                                          c system-definition)
                                  (return-from :test-system))))
                        (unless (and (or (typep system-pathname 'logical-pathname)
                                         (typep module-pathname 'logical-pathname))
                                     (and (stringp file-pathname) (find #\. file-pathname)))
                          (incf system-count)
                          (let* ((system (eval system-definition))
                                 (module (first (asdf:module-components system)))
                                 (file-components (asdf:module-components module)))
                            (incf file-count (length file-components))
                            (incf directory-count 2)
                            (labels ((translate-if-needed (pathname)
                                       (if (typep pathname 'logical-pathname)
                                         (cons (translate-logical-pathname pathname) pathname)
                                         pathname))
                                     (test-module (module)
                                       (incf directory-count)
                                       (unless #-clisp (probe-file (asdf:component-pathname module))
                                               #+clisp (ext:probe-directory (asdf:component-pathname module))
                                         (incf directory-failures)
                                         (push (list (type-of module) (asdf:component-name module)
                                                     (translate-if-needed (asdf:component-pathname module))
                                                     configuration
                                                     (list (when (asdf:component-parent module) (asdf:component-pathname  (asdf:component-parent module)))))
                                               failures)))
                                     (test-file (file)
                                       (incf file-count)
                                       (unless (ignore-errors
                                                (with-open-file (stream (asdf:component-pathname file) :direction :output :if-exists :supersede :if-does-not-exist :error)
                                                  (print start-time stream)))
                                         (incf file-failures)
                                         (push (list (type-of file) (asdf:component-name file)
                                                     (translate-if-needed (asdf:component-pathname file))
                                                     configuration
                                                     (list (asdf:component-pathname  (asdf:component-system file))
                                                           (asdf:component-pathname  (asdf:component-parent file))))
                                               failures))))
                              (test-module system)
                              (test-module module)
                              (dolist (file file-components) (test-file file)))))))))))

            (format result-stream "% target files [s]:{% s -> s}%"
                    (length test-files)
                    (mapcar #'(lambda (file)
                                (list file (if (probe-file file)
                                             (if (> (file-write-date file) start-time)
                                               :ok
                                               :untouched)
                                             :missing)))
                            test-files))

            (format result-stream "&%% translations: a: s" "ASDFTEST" (logical-pathname-translations "ASDFTEST"))

            (format result-stream "&%% variations:% systems: s% modules: s% files: s"
                    systems modules files)

            (let ((homogeneous-failures 0) (*print-length* nil))
              (format result-stream "&%% pathname failures [s]:" (length failures))
              (dolist (failure failures)
                (destructuring-bind (type name intended-pathname configuration parent-pathnames) failure
                  (format result-stream "&%:[ ;!]a24Ts%  missing:24Ts%  configuration:24Ts%  parent pathnames:24Ts"
                          (flet ((logical-p (p) (typep p 'logical-pathname)))
                            (when (or (every #'logical-p configuration) (notany #'logical-p configuration))
                              (incf homogeneous-failures)))
                          type name intended-pathname configuration parent-pathnames)))
              (terpri result-stream)
              (print (print `(:result :type ,(lisp-implementation-type) :version ,(lisp-implementation-version)
                                      :file ,(pathname result-stream)
                                      :system-failures (,system-failures ,system-count)
                                      :directory-failures (,directory-failures ,directory-count)
                                      :file-failures (,file-failures ,file-count)
                                      :homogeneous ,homogeneous-failures)
                            result-stream)
                     *trace-output*)
              (terpri *trace-output*)
              (force-output *trace-output*)
              (and (zerop system-failures) (zerop directory-failures)
                   (zerop file-failures) (zerop homogeneous-failures)))))))
    (when delete-host
      (setf (logical-pathname-translations "ASDFTEST") nil))
    (remhash "test-system" asdf::*defined-systems*)))

(quit-on-error
 (asdf:initialize-source-registry)
 (format t "source registry: S%" (asdf::source-registry))
 (asdf:initialize-output-translations)
 (format t "output translations: S%" (asdf::output-translations))

 (or (test-component-pathnames :delete-host t :support-string-pathnames nil)
     (leave-lisp "test failed" 1)))

;;; (load "LIBRARY:de;setf;utility;asdf;cp-test.lisp")
;;; (logical-pathname-translations "ASDFTEST")
;;; (gethash "test-system" asdf::*defined-systems*)
